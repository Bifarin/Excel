



Sub Call_Out_Selected()
    ' Keyboard Shortcut: Ctrl+Shift+C
    '

    ' Check if there is a selected shape
    If ActiveWindow.Selection.Count = 0 Then
        MsgBox "Please select a shape before running this macro.", vbExclamation
        Exit Sub
    End If

    ' Enable diagram services
    Dim DiagramServices As Integer
    DiagramServices = ActiveDocument.DiagramServicesEnabled
    ActiveDocument.DiagramServicesEnabled = visServiceVersion140 + visServiceVersion150

    ' Get the selected shape
    Dim selectedShape As Visio.Shape
    Set selectedShape = ActiveWindow.Selection(1)


    Dim vsoDoc1 As Visio.Document
    Set vsoDoc1 = Application.Documents.OpenEx(Application.GetBuiltInStencilFile(visBuiltInStencilCallouts, visMSUS), visOpenHidden)

    
    Dim UndoScopeID1 As Long
    UndoScopeID1 = Application.BeginUndoScope("Drop Callout")

    ' Drop Callout on the selected shape
    Application.ActivePage.DropCallout vsoDoc1.Masters.ItemU("Thought Bubble"), selectedShape

    Application.EndUndoScope UndoScopeID1, True
    vsoDoc1.Close

    ' Restore diagram services
    ActiveDocument.DiagramServicesEnabled = DiagramServices

End Sub

